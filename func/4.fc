{-

  TASK 4 - (De)Serialize to Cell
  Write the methods
    a) for serialization of tuple of arbitrary values of different types to Cell
    b) for deserialization of Cell created by method above to original tuple

  `serialize` method gets as input tuple with arbitrary number of elements from 0
  to 128 (both 0 and 128 elements are allowed) and outputs Cell. Elements of the
  tuple may be `null`, `int`, `cell`, `slice`, `tuple` (with the same limitations
  as original tuple). It is guaranteed that the maximum nesting level of tuples
  is less than 4 and total number of elements less than 1024.
  `deserialize` method gets a cell produced by `serialize` method and should
  return origin tuple.

  Note, for illustrative purposes`serialize_t3` and `deserialize_t3` functions
  which serialize and deserialize tuple with exactly 3 elements
  (only null, int, cell and slice are supportd) to/from a cell  have been added.
  Participants are free to not use logic from there and come up with their own
  implementations.

  Tests will only check that test_tuple == deserialize(serialize(test_tuple)).
  Intermediate representation can be arbitrary.
-}

cell tuple_at_index(tuple t, int index) asm "INDEXVAR";
forall X -> cell force_to_var(X x) asm "NOP";

() recv_internal() {
}

const int key_len = 8; ;; tuple can't be long anyway

builder serialize_element(var x) {
  builder b = begin_cell();
  
  if(is_null(x)) {
    return b.store_uint(0,3);
  }
  if(is_int(x)) {
    int i_x = force_cast_to_int(x);
    return b.store_uint(1,3).store_int(i_x, 257);
  }
  if(is_cell(x)) {
    cell c_x = force_cast_to_cell(x);
    return b.store_uint(2,3).store_ref(c_x);
  }
  if(is_slice(x)) {
    slice s_x = force_cast_to_slice(x);
    return b.store_uint(3,3).store_ref(begin_cell().store_slice(s_x).end_cell());
  }
  if (is_tuple(x)) {
    cell d = new_dict();
    tuple values = force_cast_to_tuple(x);
    int len = tuple_length(values);
    int i = 0;
    while (i < len) {
      d~udict_set_builder(key_len, i,
        serialize_element(values.tuple_at_index(i))
      );
      i += 1;
    }
    
    return b.store_uint(4,3).store_dict(d);
  }
  
  throw(1900);
  return b;
}

;; testable
(cell) serialize(tuple values) method_id {
  return serialize_element(force_to_var(values)).end_cell();
}

var deserialize_element(slice cs) {
  int element_type = cs~load_uint(3);
  if(element_type == 0) {
    return null();
  }
  if(element_type == 1) {
    return force_to_var(cs~load_int(257));
  }
  if(element_type == 2) {
    return cs~load_ref();
  }
  if(element_type == 3) {
    return force_to_var(cs~load_ref().begin_parse());
  }
  if (element_type == 4) {
    cell d = cs~load_dict();
    tuple t = empty_tuple();
    
    int key = -1;
    do {
      (key, slice value, int f) = d.udict_get_next?(key_len, key);
      if (f) {
        t~tpush(deserialize_element(value));
      }
    } until (~ f);
    
    return force_to_var(t);
  }
  
  throw(2900);
  return begin_cell().end_cell();
}

;; testable
(tuple) deserialize(cell serialized) method_id {
  return force_cast_to_tuple(
    deserialize_element(serialized.begin_parse())
  );
}

